extends Node2D

var plant_scene = preload("res://scenes/objects/plant.tscn")
var plant_info_scene = preload("res://scenes/ui/plant_info.tscn")
var raining: bool:
	set(value):
		raining = value
		$Layers/Particles/GPUParticles2D.emitting = value
		$Layers/Particles/GPUParticles2D2.emitting = value
var used_cells: Array[Vector2i]
var projectile_scene = preload("res://scenes/machines/projectile.tscn")
var machine_scenes = {
	Enum.Machine.SPRINKLER: preload("res://scenes/machines/sprinkler.tscn"),
	Enum.Machine.SCARECROW: preload("res://scenes/machines/scarecrow.tscn"),
	Enum.Machine.FISHER: preload("res://scenes/machines/fisher.tscn")
}
var blob_scene = preload("res://scenes/characters/blob.tscn")

const MACHINE_PREVIEW_TEXTURES = {
	Enum.Machine.SPRINKLER: {'texture':preload("res://graphics/icons/sprinkler.png"), 'offset': Vector2i(0,0)},
	Enum.Machine.FISHER: {'texture':preload("res://graphics/icons/fisher.png"), 'offset': Vector2i(0,-4)},
	Enum.Machine.SCARECROW: {'texture':preload("res://graphics/icons/scarecrow.png"), 'offset': Vector2i(0,-4)},
	Enum.Machine.DELETE: {'texture':preload("res://graphics/icons/delete.png"), 'offset': Vector2i(0,0)}}
	

@export var rain_color: Color
@export var daytime_color: Gradient
@onready var daytransition_material = $Overlay/CanvasLayer/DayTransitionLayer.material
@onready var player: CharacterBody2D = $Objects/Player

signal day_restarted()

func _on_player_tool_use(tool: Enum.Tool, pos: Vector2) -> void:
	var grid_coord: Vector2i = Vector2i (pos.x/Data.TILE_SIZE, pos.y/Data.TILE_SIZE)
	grid_coord.x += -1 if pos.x < 0 else 0
	grid_coord.y += -1 if pos.y < 0 else 0
	var has_soil = grid_coord in $Layers/SoilMapLayer.get_used_cells()
	match tool:
		Enum.Tool.HOE:
			var cell = $Layers/GrassMapLayer.get_cell_tile_data(grid_coord) as TileData
			if cell:
				if cell.get_custom_data('farmable'):
					$Layers/SoilMapLayer.set_cells_terrain_connect([grid_coord], 0, 0)
					if raining:
						$Layers/WateredSoilMapLayer.set_cell(grid_coord, 0, Vector2i(randi_range(0, 2), 0))
		Enum.Tool.WATER:
			if has_soil:
				$Layers/WateredSoilMapLayer.set_cell(grid_coord, 0, Vector2i(randi_range(0, 2), 0))
				pass
		Enum.Tool.FISH:
			if not grid_coord in $Layers/GrassMapLayer.get_used_cells():
				$Objects/Player.start_fishing()
		Enum.Tool.SEED:
			if has_soil and grid_coord not in used_cells:
				var plant_res = PlantResource.new()
				plant_res.setup($Objects/Player.current_seed)
				var plant = plant_scene.instantiate()
				plant.death.connect(_on_plant_death)
				plant.setup(grid_coord, $Objects, plant_res)
				used_cells.append(grid_coord)
				
				var plant_info = plant_info_scene.instantiate()
				plant_info.setup(plant_res)
				$Overlay/CanvasLayer/PlantInfoContainer.add(plant_info)
		Enum.Tool.AXE:
			for object in get_tree().get_nodes_in_group('Objects'):
				if object.position.distance_to(pos) < 20:
					object.hit(tool)
		Enum.Tool.SWORD:
			for object in get_tree().get_nodes_in_group('ObjectsS'):
				if object.position.distance_to(pos) < 20:
					object.hit(tool)

func _ready() -> void:
	Data.forecast_rain = [true, false].pick_random()

func _process(_delta: float) -> void:
	var daytime_point = 1 - ($Timers/DayTimer.time_left / $Timers/DayTimer.wait_time)
	var color: Color
	
	if raining:
		color = daytime_color.sample(daytime_point).lerp(rain_color, 0.5)
	else:
		color = daytime_color.sample(daytime_point)
	$Overlay/DayTimeColor.color = color
	
	$Overlay/MachinePreviewSprite.visible = player.current_state == Enum.State.BUILDING
	$Overlay/MachinePreviewSprite.position = player.get_machine_coord()
	


func day_restart():
	var tween = create_tween()
	tween.tween_property(daytransition_material, "shader_parameter/progress", 1.0, 1.0)
	tween.tween_interval(0.5)
	tween.tween_callback(level_reset)
	tween.tween_property(daytransition_material, "shader_parameter/progress", 0.0, 1.0)

func level_reset():
	day_restarted.emit()
	$Timers/DayTimer.start()
	for plant in get_tree().get_nodes_in_group("Plants"):
		plant.grow(plant.coord in $Layers/WateredSoilMapLayer.get_used_cells())
	$Layers/WateredSoilMapLayer.clear()
	$Overlay/CanvasLayer/PlantInfoContainer.update_all()
	raining = Data.forecast_rain
	Data.forecast_rain = [true, false].pick_random()
	if raining:
		for cell in $Layers/SoilMapLayer.get_used_cells():
			$Layers/WateredSoilMapLayer.set_cell(cell, 0, Vector2i(randi_range(0, 2), 0))


func _on_plant_death(coord: Vector2i):
	used_cells.erase(coord)



func _on_player_diagnose() -> void:
	$Overlay/CanvasLayer/PlantInfoContainer.visible = not $Overlay/CanvasLayer/PlantInfoContainer.visible


func _on_player_day_change() -> void:
	day_restart()

func create_projectile(start_pos: Vector2, dir: Vector2):
	pass
	var projectile = projectile_scene.instantiate()
	projectile.setup(start_pos, dir)
	$Objects.add_child(projectile)


func _on_player_build(current_machine: int) -> void:
	if current_machine != Enum.Machine.DELETE:
		var machine = machine_scenes[current_machine].instantiate()
		machine.setup(player.get_machine_coord(), self, $Objects)
		if machine.has_signal("shoot_projectile"):
			machine.connect("shoot_projectile", create_projectile)
	else:
		for machine in get_tree().get_nodes_in_group("Machines"):
			machine.delete(player.get_machine_coord() / 16)


func _on_player_machine_changed(current_machine: int) -> void:
	$Overlay/MachinePreviewSprite.texture = MACHINE_PREVIEW_TEXTURES[current_machine]['texture']

func water_plants(coord: Vector2i):
	const SOIL_DIRECTIONS = [
		Vector2i(-1, -1), Vector2i(0, -1), Vector2i(1, -1),
		Vector2i(-1,  0),Vector2i(1,0), Vector2i(-1,  1), 
		Vector2i(0,  1), Vector2i(1,  1)]
	for dir in SOIL_DIRECTIONS:
		var cell = coord + dir
		if cell in $Layers/SoilMapLayer.get_used_cells():
			$Layers/WateredSoilMapLayer.set_cell(cell, 0, Vector2i(randi_range(0, 2), 0), 0)


func _on_blob_timer_timeout() -> void:
	var plants = get_tree().get_nodes_in_group("Plants")
	if plants:
		var blob = blob_scene.instantiate()
		blob.setup($BlobSpawnPositions.get_children().pick_random().position, plants.pick_random(), $Objects)
