extends CharacterBody2D

@onready var msm = $Animation/AnimationTree.get("parameters/MoveStateMachine/playback")
@onready var tsm = $Animation/AnimationTree.get("parameters/ToolStateMachine/playback")

var direction: Vector2
var last_direction: Vector2
var speed := 150
var current_tool: Enum.Tool
var current_seed: Enum.Seed
var can_move: bool = true
var current_state: Enum.State
var current_style: Enum.Style
var current_machine: Enum.Machine
signal day_change
signal diagnose
signal tool_use(tool: Enum.Tool, pos:Vector2)
signal build(current_machine: Enum.Machine)
signal machine_changed(current_machine: Enum.Machine)

func move():
	direction = Input.get_vector("left", "right", "up", "down")
	velocity = direction * speed
	move_and_slide()

func get_basic_input():
	if Input.is_action_just_pressed("tool_backward") or Input.is_action_just_pressed("tool_forward"):
		var dir = Input.get_axis("tool_backward", "tool_forward")
		current_tool = posmod((current_tool + int(dir)), Enum.Tool.size()) as Enum.Tool
		$ToolUI.reveal()
	if Input.is_action_just_pressed("seed_forward"):
		current_seed = posmod(current_seed + 1, Enum.Seed.size()) as Enum.Seed
		$SeedsUI.reveal()
	if Input.is_action_just_pressed("diagnose"):
		diagnose.emit()

	if Input.is_action_just_pressed("action"):
		if not $RayCast2D.get_collider():
			tsm.travel(Data.TOOL_STATE_ANIMATIONS[current_tool])
			$Animation/AnimationTree.set("parameters/ToolOneShot/request", AnimationNodeOneShot.ONE_SHOT_REQUEST_FIRE)
		else:
			$RayCast2D.get_collider().interact(self)
	
	if Input.is_action_just_pressed("style_toggle"):
		current_style = posmod(current_style + 1, Enum.Style.size() - 1) as Enum.Style
		$Sprite2D.texture = Data.PLAYER_SKINS[current_style]
	
	if Input.is_action_just_pressed("build"):
		current_state = Enum.State.BUILDING
		machine_changed.emit(current_machine)

func get_fishing_input():
	if Input.is_action_just_pressed("action"):
		$FishingGame.raise_bar()

func get_building_input():
	if Input.is_action_just_pressed("build"):
		current_state = Enum.State.DEFAULT
	if Input.is_action_just_pressed("tool_backward") or Input.is_action_just_pressed("tool_forward"):
		var dir = Input.get_axis("tool_backward", "tool_forward")
		current_machine = posmod((current_machine + int(dir)), Enum.Machine.size()) as Enum.Machine
		machine_changed.emit(current_machine)
	if Input.is_action_just_pressed("action"):
		build.emit(current_machine)
		

func start_fishing():
	$Animation/AnimationTree.set("parameters/FishBlend/blend_amount", 1)
	current_state = Enum.State.FISHING
	$FishingGame.reveal()

func stop_fishing():
	current_state = Enum.State.DEFAULT
	$Animation/AnimationTree.set("parameters/FishBlend/blend_amount", 0)

func animate():
	if direction:
		msm.travel("Walk")
		var animation_direction := Vector2(round(direction.x), round(direction.y))
		$Animation/AnimationTree.set("parameters/MoveStateMachine/Idle/blend_position", animation_direction)
		$Animation/AnimationTree.set("parameters/MoveStateMachine/Walk/blend_position", animation_direction)
		$Animation/AnimationTree.set("parameters/FishBlendSpace2D/blend_position", animation_direction)
		for animation in Data.TOOL_STATE_ANIMATIONS.values():
			var animation_name: String =("parameters/ToolStateMachine/"+ animation +"/blend_position")
			$Animation/AnimationTree.set(animation_name, animation_direction)
	else:
		msm.travel("Idle")

func tool_use_emit():
	tool_use.emit(current_tool, position + last_direction * 16 + Vector2(0, 4))

func _physics_process(_delta: float) -> void:
	match current_state:
		Enum.State.DEFAULT:
			if can_move:
				get_basic_input()
				move()
				animate()
		Enum.State.FISHING:
			get_fishing_input()
		Enum.State.BUILDING:
			if can_move:
				get_building_input()
				move()
				animate()
	
	if direction:
		last_direction = direction
		var ray_y = int(direction.y) if not direction.x else 0
		$RayCast2D.target_position = Vector2(direction.x, ray_y).normalized() * 20


func _on_animation_tree_animation_started(_anim_name: StringName) -> void:
	can_move = false

func _on_animation_tree_animation_finished(_anim_name: StringName) -> void:
	can_move = true

func get_machine_coord() -> Vector2i:
	var pos = position + last_direction * 20 + Vector2(0, 8)
	var coord = Vector2i(pos.x / Data.TILE_SIZE, pos.y / Data.TILE_SIZE)
	coord.x += -1 if pos.x < 0 else 0
	coord.y += -1 if pos.y < 0 else 0
	return coord * Data.TILE_SIZE + Vector2i(8, 8)
